// Copyright (c) 2022 Lachlan McDonald
// This work is licensed under a Creative Commons
// Attribution-NonCommercial-ShareAlike 3.0 Unported License (CC BY-NC-SA 3.0)
// https://github.com/lachlanmcdonald/magicavoxel-shaders
//
// This script utilises or modifies code from other projects or publications.
// Please see the attributions below for more information:
//
// 1. "Hex grid traversal" by mattz
//    Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License
//    https://www.shadertoy.com/view/XdSyzK
//
// xs brush/hex_tiles [Scale] [Outline] [Seed]
//
// xs_begin
// author : '@lachlanmcdonald'
// arg : { name = 'Scale'  var = 'm_scale'  range = '1 100'  value = '10'  step = '1'  precision = '0' }
// arg : { name = 'Outline'  var = 'm_outline'  range = '-100 100'  value = '0'  step = '1'  precision = '0' }
// arg : { name = 'Seed'  var = 'global_seed'  range = '1 100'  value = '1'  step = '1'  precision = '0' }
// xs_end

float HEX_FACTOR = 0.8660254037844386;
float HASH_SCALE = 0.1031;

float outline = m_outline / 100.0;
float scale = m_scale / 2.0;

vec2 hexFromCart(vec2 p) {
	return vec2(p.x / HEX_FACTOR, p.y);
}

vec2 cartFromHex(vec2 g) {
	return vec2(g.x * HEX_FACTOR, g.y);
}

vec2 nearestHexCell(in vec2 pos) {
	vec2 gpos = hexFromCart(pos);
	vec2 hex_int = floor(gpos);

	float sy = step(2.0, mod(hex_int.x + 1.0, 4.0));
	hex_int += mod(vec2(hex_int.x, hex_int.y + sy), 2.0);

	vec2 gdiff = gpos - hex_int;

	if (dot(abs(gdiff), vec2(HEX_FACTOR * HEX_FACTOR, 0.5)) > 1.0) {
		vec2 delta = sign(gdiff) * vec2(2.0, 1.0);
		hex_int += delta;
	}

	return hex_int;
}

float hexDist(vec2 p) {
	p = abs(p);
	return max(dot(p, vec2(HEX_FACTOR, 0.5)), p.y) - 1.0;
}

float hash12(vec2 p) {
	vec3 p3 = fract(vec3(p.xyx) * HASH_SCALE);

	p3 += dot(p3, p3.yzx + 19.19);
	return fract((p3.x + p3.y) * p3.z);
}

struct Cell {
	float h;
	float uv;
	vec2 pos;
};

Cell find_cell(vec2 pos, float seed) {
	vec2 cell_pos = nearestHexCell(pos);
	float cell_uv = abs(hexDist(pos - cartFromHex(cell_pos)));
	float c = hash12(cell_pos + seed);
	return Cell(c, cell_uv, cell_pos);
}

float pal(float p) {
	float f = floor(mix(0.0, float(i_num_color_sels), p));
	return color_sel(f);
}

float map(vec3 v) {
	vec3 uv = floor(v) / i_volume_size - 0.5;
	Cell hex = find_cell(uv.xy * m_scale, global_seed);

	float k = uv.z < hex.h ? pal(hex.h) : 0.0;

	if (outline > 0.0) {
		if (outline <= hex.uv) {
			return k;
		} else {
			return 0.0;
		}
	} else if (outline < 0.0) {
		if (abs(outline) > hex.uv) {
			return k;
		} else {
			return 0.0;
		}
	} else {
		return k;
	}
}
